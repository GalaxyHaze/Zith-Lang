## 4. Traits, Interfaces & Capabilities

> **Implementation status:** `trait`, `interface`, and `implement T as Trait {}` declarations are
> **working** (parsed, resolved, and type-checked). Trait conformance is nominal: an implementation
> is validated against every required method and the conformance edge is recorded for generic
> bounds. Interfaces are structural: a concrete struct satisfies an interface automatically when
> every declared field exists with the required type and every declared method requirement has a
> compatible signature. Using an interface as a generic bound exposes the interface fields and
> methods to the generic body. Trait defaults are resolved for concrete owners during sema; `dyn
> Trait`, dynamic dispatch, `requires`/`extends` as explicit constraints, and per-owner
> default-method HIR generation remain spec-only or pending. `Self` in implementations and trait
> defaults resolves to the implementing type.

The `implement` owner may be a primitive, `?T`, or `[]T` in addition to a named struct/enum:

```zith
trait Describe {
    fn describe(self): i32;
}

implement i32 as Describe {
    fn describe(self): i32 { return 1; }
}
```

Traits implemented for these owners participate in nominal conformance exactly like named types, so they are usable in generic bounds (`T: Describe`) and expose default methods on the receiver. Pointer and fixed-array owners remain unsupported.

> See [impl-status.md](impl-status.md).

### 4.1 Traits vs. Interfaces

| | Trait | Interface |
|---|---|---|
| **Typing** | Nominal — must be explicitly implemented. | Structural (duck-typed) — automatically satisfied when fields match. |
| **Extensible** | Yes — via `extends`, or as a precondition using `requires`. | No — interfaces cannot extend each other, though a trait may `requires` one. |
| **Has implementation?** | Yes — default method bodies are allowed. | No — declaration only. |
| **Field access** | Only through a trait that `requires` the interface. | Yes — directly, since interfaces are structural. |
| **Methods** | Default bodies and requirements. | Declaration-only requirements, no default bodies. |

### 4.2 Traits

A `requires Cond` clause goes **before** `trait` or `interface`. It forces any implementing type to also satisfy that condition. Traits may provide default method bodies. Use `self` / `other` as the conventional instance parameters, and `Self` (capitalized) for the concrete implementing type.

```zith
trait Printable {
    fn print(self);
    fn println(self) { self.print(); io.writeln(""); }
}

requires Printable
trait JsonSerializable {
    fn print(self);
    fn toJson(self): string;
}

// Disambiguate overlapping method names using the trait as a namespace
Printable.print(self);
JsonSerializable.print(self);
```

### 4.3 Interfaces

Interfaces are structural — if it quacks, it's a duck. Any type that has the required fields and
compatible method signatures satisfies the interface automatically, without an explicit
`implement` declaration. Interfaces accept declaration-only method requirements and both single
and grouped field forms. You can also add `requires` to interfaces.

```zith
// will only accept structs and reject components
requires @isStruct
interface iPositioned {
    x: f32,
    [y, z]: f32,
    fn length2(self): f32
}

requires iPositioned
trait Movable {
    fn translate(self, dx: f32, dy: f32, dz: f32) {
        self.x += dx; self.y += dy; self.z += dz;
    }
}

// Any struct with x, y, z: f32 and length2(self): f32 satisfies iPositioned
struct Enemy {
    [x, y, z]: f32,
    health: i32,
    fn length2(self): f32 { self.x * self.x + self.y * self.y + self.z * self.z }
}

fn distance2<T: iPositioned>(p: T): f32 { p.length2() }
```

### 4.4 Capabilities — Built-in Reference

Capabilities are special traits that feed the compiler more information, unlocking special rules and optimizations.

| Capability | What it does |
|---|---|
| `Copy` | Implicit bitwise copy. Components and primitives are `Copy` by default. |
| `Functor` | `operator()` — makes a type callable like a function. |
| `Arithmetic` | Operators `+`, `-`, `*`, `/`, `%`, and so on. |
| `Error` | `operator throw`, required for `throw MyError;`. |
| `Null` | A negative capability — its traits activate only once NRA has proven a value IS `null`. Outside the proven-null branch, calling the method is a **compile error**. |
| `Fail` | A negative capability — its traits activate only once NRA has proven a value IS an error. Cannot coexist with `Null` on the **same level**, but `?T!` can have `Null` on the outer level and `Fail` on the inner. |
| `Allocator` | To provide custom allocators |
| `Generator` | Allows creating runtime-defined resumable or streaming protocols without introducing a dedicated core function kind. |
| `Share` | Required for `global: share` and crossing thread boundaries |
| `ThreadBackend` | Provides a concrete thread handle for explicit `fork`/`merge`, e.g. `pThread` |
| `Lent` | Enables `global: unique`, a runtime-checked exclusive borrow. `global` bindings cannot be moved — `Lent` manages thread-safe distribution. Also allows `lend` parameters. |
| `Trust` | A trait extending `Trust` may contain `raw fn` methods callable from safe contexts. |
| `Unique` | Marks a singleton type. It cannot be instantiated — the type name itself acts as the instance. All fields must implement `Share` (thread-safe). A `unique Local` variant is a singleton thread-local. |

#### `Null` & `Fail` — Negative Capabilities

Dispatch is based on NRA state. Inside a proven-null branch, the `Null` trait unlocks. Outside it, calling `Null` methods is a **compile error**:

```zith
implement Config as Null {
    fn onMissing(self) { log("Config was null -- using defaults"); }
}
implement Config! as Fail {
    fn onError(self) { log("Config load failed"); }
}

let cfg: ?Config = loadConfig();
if (cfg is null) {
    cfg.onMissing();   // OK — Null trait unlocked here
}
// cfg.onMissing();   -- COMPILE ERROR: outside null branch

// Multi-level: ?T! — Null on outer, Fail on inner
// cfg.onError() is only valid inside a proven-error branch
```

`Null` and `Fail` are per-level. A `?T!` value can activate `Null` (outer `?`) independently from `Fail` (inner `!`).

#### `Trust` — Safe Sections with Raw Code

```zith
trait Place extends Trust {
    raw fn sample(): i32 {}
}

fn safeCaller(a: Place) {
    let v = a.sample();
}
```

#### `Unique` — Singleton Types

```zith
struct AppConfig: Unique { host: string, port: u16 }

AppConfig.host = "localhost";
AppConfig.port = 8080;
// let cfg = AppConfig { ... };  -- COMPILE ERROR
```

#### `Share` — Mutable State Across Thread Boundaries

```zith
// Share is mutable, multiple names, multiple threads, all can write
global counter: share Atomic<i32> = 0;

// Without the Share capability, cross-thread access is a compile error
struct LocalOnly { data: i32 }
// global bad: share LocalOnly = ...;  -- COMPILE ERROR: lacks Share
```

#### `ThreadBackend` — Thread Fork/Merge

`ThreadBackend` is the runtime side of explicit thread fork/merge. A backend
object such as `pThread` creates a concrete `Thread<T>` handle; `merge` then
blocks and consumes that handle once:

```zith
let t: PThreadHandle<i32> = pThread fork Update(share state, n);
let result: i32 = merge t;
```

`fork` is a core keyword that names the entry action and the backend object;
`spawn Entry(args)` is a stdlib shorthand for the active backend. There is no
`await`, future, or resumable task in this protocol. The returned value is
exactly the result type declared by the entry action, including failable types
when the action can fail. See [the branch protocol plan](plans/branch-protocol.md)
for the full design.

### 4.5 Operator Overloading

Operator overloading is capability-based and strict — you implement only the operators you need:

```zith
implement Vec3 as Arithmetic {
    fn +(self, other: Self): Self { ... }
    fn -(self, other: Self): Self { ... }
}

implement Pipeline as Functor {
    fn (self, input: []u8): []u8 { ... }
}
let out = pipe(raw_bytes);
```

### 4.6 Extension (`extends`)

`extends` copies the base type's fields and traits into the new struct. An optional `:` after the base lists further traits to implement:

```zith
struct Dog extends Animal {}
struct T extends Base: Transform, Collision {}

// Traits may also extend capabilities or other traits
trait SafeBuffer extends Trust {
    raw fn readByte(self, offset: u64): u8 {}
}
```

---

*[Zith Language Specification](Zith-spec.md) — Draft v0.9*
